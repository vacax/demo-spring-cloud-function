
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'org.springframework.boot.experimental.thin-launcher' version "1.0.31.RELEASE"
}

group = 'edu.pucmm.eict'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    set('springCloudVersion', "2023.0.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-function-context'
    implementation 'org.springframework.cloud:spring-cloud-function-web'
    implementation "org.springframework.cloud:spring-cloud-function-adapter-aws"
    implementation 'com.amazonaws:aws-lambda-java-events:2.0.2'
    implementation 'com.amazonaws:aws-lambda-java-core:1.1.0'
    //implementation 'com.microsoft.azure.functions:azure-functions-java-library:1.4.2'
    //implementation 'org.springframework.cloud:spring-cloud-starter-aws'
    //implementation 'org.springframework.cloud:spring-cloud-starter-aws-jdbc'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

assemble.dependsOn shadowJar

//
import com.github.jengelman.gradle.plugins.shadow.transformers.*

shadowJar.mustRunAfter thinJar

//Creando la Jar listo para publicar en AWS.
shadowJar {
    archiveClassifier = 'aws'
    manifest {
        inheritFrom(project.tasks.thinJar.manifest)
    }
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    append 'META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
    append 'META-INF/spring/org.springframework.boot.actuate.autoconfigure.web.ManagementContextConfiguration.imports'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
}



test {
    useJUnitPlatform()
}

/*azurefunctions {
    resourceGroup = 'java-functions-group'
    appName = 'azure-functions-sample'
    pricingTier = 'Consumption'
    region = 'eastus'
    auth {
        type = 'azure_cli'
    }
    localDebug = "transport=dt_socket,server=y,suspend=n,address=5005"
}*/
